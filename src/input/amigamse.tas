
;GEOS Amiga style mouse input driver
;reassembled by Maciej 'YTM/Elysium' Witkowiak
;29.07.2001

		;*=MOUSE_JMP

MouseJMP	JMP _MouseInit
SlowMouse	JMP _SlowMouse
UpdateMouse	JMP _UpdateMouse

acceleration	!byte 3

fireLast	!byte 0

lastO		!byte 1
lastY		!byte 1
lastX		!byte 1

goingUp		!byte $01,$03,$02
goingDown	!byte $00,$02,$03,$01


_MouseInit	LDA #8
		STA mouseXPos
		STA mouseYPos
		LDA #0
		STA mouseXPos+1
		STA menuNumber
_SlowMouse	RTS

_UpdateMouse	+bbrf MOUSEON_BIT, mouseOn, _SlowMouse
		+PushB CPU_DATA
		+LoadB CPU_DATA, IO_IN
		+PushB cia1base+3
		+PushB cia1base+1
		LDA mouseAccel
		LSR
		LSR
		LSR
		LSR
		STA acceleration
		+LoadB cia1base+3, $ff
		LDA cia1base+1
		AND #%00010000
		CMP fireLast
		BEQ cont
		STA fireLast
		LDA cia1base+1
		AND #%00010000
		BNE cont
		+smbf MOUSE_BIT, pressFlag
		ASL
		ASL
		ASL
		STA mouseData

cont		LDA cia1base+1
		TAY
		AND #%00000001
		STA lastO
		TYA
		AND #%00000100
		LSR
		ORA lastO
		TAX
		PHA
		CMP lastY
		BEQ hop1
		LDA lastY
		CMP goingDown,X
		BEQ goDown
		CMP goingUp,X
		BNE hop1

goUp		TXA
		PHA
		LDA mouseYPos
		+sub acceleration
		CMP #200
		BCC hophop1
		LDA #0
		JMP hophop1

goDown		TXA
		PHA
		LDA mouseYPos
		+add acceleration
		CMP #200
		BCC hophop1
		LDA #199

hophop1		STA mouseYPos
		PLA
		TAX

hop1		+PopB lastY

;OUSE_LR
		TYA
		AND #%00000010
		LSR
		STA lastO
		TYA
		AND #%00001000
		LSR
		LSR
		ORA lastO
		TAX
		PHA
		CMP lastX
		BEQ hop2
		LDA lastX
		CMP goingUp,X
		BEQ goUp2
		CMP goingDown,X
		BNE hop2

goDown2		LDA mouseXPos
		+add acceleration
		STA mouseXPos
		LDA mouseXPos+1
		ADC #0
		STA mouseXPos+1
		BEQ hop2
		LDA mouseXPos
		CMP #$40
		BCC hop2
		LDA #$3F
		STA mouseXPos
		JMP hop2

goUp2		LDA mouseXPos
		+sub acceleration
		STA mouseXPos
		LDA mouseXPos+1
		SBC #0
		STA mouseXPos+1
		BPL hop2
		+LoadW mouseXPos, 0

hop2		+PopB lastX
		+PopB cia1base+1
		+PopB cia1base+3
		+PopB CPU_DATA
		RTS
