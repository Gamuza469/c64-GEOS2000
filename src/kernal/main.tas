
;26.6.99 - modularized source, seems to work flawlessly
;this file:system core - all address-aligned data (for compatibility), core (EnterDeskTop) and init stuff


;GEOS Kernal
;reassembled by Maciej 'YTM/Alliance' Witkowiak
;5-9,13.2.99, 1-7.3.99, 12.3.99, 15-20.3.99
;changes 30.3, 3.4
;18.8 - ACME port

;bug in alternate version of Menu_5 - there's no delay between item inversion
;5.4 - ramSwapFile support
;5,6.4 - ramEnterDeskTop support
;20.4  - 2MHz support

;--------------------------------------------
;IMPORTANT! DO NOT CHANGE ANYTHING BELOW, UP TO 'DESK TOP' STRING.
;DOING SO WILL CREATE FATAL INCOMPATIBILITES!
;--------------------------------------------

		JMP BootKernal			;C01B
		JMP InitKernal			;5000

bootName	!text "GEOS BOOT"		;c006
version 	!byte $20			;c00f
nationality	!byte $00,$00
sysFlgCopy	!byte $00			;c012
c128Flag	!byte $00
		!byte $05,$00,$00,$00
dateCopy	!byte 88,4,20			;c018


;--------------------------------------------

BitMask1	!byte $80, $40, $20, $10, $08, $04, $02
BitMask2	!byte $01, $02, $04, $08, $10, $20, $40, $80
BitMask3	!byte $00, $80, $c0, $e0, $f0, $f8, $fc, $fe
BitMask4	!byte $7f, $3f, $1f, $0f, $07, $03, $01, $00

						;c418
VIC_IniTbl	!byte $00, $00, $00, $00, $00, $00, $00, $00
		!byte $00, $00, $00, $00, $00, $00, $00, $00
		!byte $f8, $3b, $fb, $aa, $aa, $01, $08, $00
		!byte $38, $0f, $01, $00, $00, $00

						;caeb
LineTabL	!byte $00, $40, $80, $c0, $00, $40, $80, $c0
		!byte $00, $40, $80, $c0, $00, $40, $80, $c0
		!byte $00, $40, $80, $c0, $00, $40, $80, $c0
		!byte $00
						;cb04
LineTabH	!byte $a0, $a1, $a2, $a3, $a5, $a6, $a7, $a8
		!byte $aa, $ab, $ac, $ad, $af, $b0, $b1, $b2
		!byte $b4, $b5, $b6, $b7, $b9, $ba, $bb, $bc
		!byte $be

_PanicDB_DT					;cfd3
		!byte DEF_DB_POS | 1
		!byte DBTXTSTR, TXT_LN_X, TXT_LN_1_Y
		!word _PanicDB_Str
		!byte NULL

_PanicDB_Str					;cfda
		!byte BOLDON
		!text "System error near $"
_PanicAddy	!text "xxxx"			;cfee
		!byte NULL

FontTVar1	!byte 0 			;e391
FontTVar2	!word 0 			;e392
						;ccb0
SprTabL 	!byte <spr0pic, <spr1pic, <spr2pic, <spr3pic
		!byte <spr4pic, <spr5pic, <spr6pic, <spr7pic
						;ccb8
SprTabH 	!byte >spr0pic, >spr1pic, >spr2pic, >spr3pic
		!byte >spr4pic, >spr5pic, >spr6pic, >spr7pic

PutCharTabL	!byte <DoBACKSPACE, <DoTAB	;e54b
		!byte <DoLF, <DoHOME
		!byte <DoUPLINE, <DoCR
		!byte <DoULINEON, <DoULINEOFF
		!byte <DoESC_GRAPHICS, <DoESC_RULER
		!byte <DoREV_ON, <DoREV_OFF
		!byte <DoGOTOX, <DoGOTOY
		!byte <DoGOTOXY, <DoNEWCARDSET
		!byte <DoBOLDON, <DoITALICON
		!byte <DoOUTLINEON, <DoPLAINTEXT

PutCharTabH	!byte >DoBACKSPACE, >DoTAB	;e55f
		!byte >DoLF, >DoHOME
		!byte >DoUPLINE, >DoCR
		!byte >DoULINEON, >DoULINEOFF
		!byte >DoESC_GRAPHICS, >DoESC_RULER
		!byte >DoREV_ON, >DoREV_OFF
		!byte >DoGOTOX, >DoGOTOY
		!byte >DoGOTOXY, >DoNEWCARDSET
		!byte >DoBOLDON, >DoITALICON
		!byte >DoOUTLINEON, >DoPLAINTEXT

DecTabL 	!byte <1, <10, <100, <1000, <10000 ;e941
DecTabH 	!byte >1, >10, >100, >1000, >10000 ;e946

daysTab 					;fe17
		!byte 31, 28, 31, 30, 31, 30
		!byte 31, 31, 30, 31, 30, 31

DeskTopOpen	!byte 0 			;these two bytes are here just
DeskTopRecord	!byte 0 			;to keep OS_JUMPTAB at $c100
		!byte 0,0,0			;three really unused
;--------------------------------------------
;here the JumpTable begins, DO NOT CHANGE!!!
;		*= OS_JUMPTAB

InterruptMain		JMP _InterruptMain	;c100
InitProcesses		JMP _InitProcesses	;c103
RestartProcess		JMP _RestartProcess	;c106
EnableProcess		JMP _EnableProcess	;c109
BlockProcess		JMP _BlockProcess	;c10c
UnBlockProcess		JMP _UnBlockProcess	;c10f
FreezeProcess		JMP _FreezeProcess	;c112
UnFreezeProcess 	JMP _UnFreezeProcess	;c115
HorizontalLine		JMP _HorizontalLine	;c118
InvertLine		JMP _InvertLine 	;c11d
RecoverLine		JMP _RecoverLine	;c11e
VerticalLine		JMP _VerticalLine	;c121
Rectangle		JMP _Rectangle		;c124
FrameRectangle		JMP _FrameRectangle	;c127
InvertRectangle 	JMP _InvertRectangle	;c12a
RecoverRectangle	JMP _RecoverRectangle	;c12d
DrawLine		JMP _DrawLine		;c130
DrawPoint		JMP _DrawPoint		;c133
GraphicsString		JMP _GraphicsString	;c136
SetPattern		JMP _SetPattern 	;c139
GetScanLine		JMP _GetScanLine	;c13c
TestPoint		JMP _TestPoint		;c13f
BitmapUp		JMP _BitmapUp		;c142
PutChar 		JMP _PutChar		;c145
PutString		JMP _PutString		;c148
UseSystemFont		JMP _UseSystemFont	;c14b
StartMouseMode		JMP _StartMouseMode	;c14e
DoMenu			JMP _DoMenu		;c151
RecoverMenu		JMP _RecoverMenu	;c154
RecoverAllMenus 	JMP _RecoverAllMenus	;c157
DoIcons 		JMP _DoIcons		;c15a
DShiftLeft		JMP _DShiftLeft 	;c15d
BBMult			JMP _BBMult		;c160
BMult			JMP _BMult		;c163
DMult			JMP _DMult		;c166
Ddiv			JMP _Ddiv		;c169
DSDiv			JMP _DSDiv		;c16c
Dabs			JMP _Dabs		;c16f
Dnegate 		JMP _Dnegate		;c172
Ddec			JMP _Ddec		;c175
ClearRam		JMP _ClearRam		;c178
FillRam 		JMP _FillRam		;c17b
MoveData		JMP _MoveData		;c17e
InitRam 		JMP _InitRam		;c181
PutDecimal		JMP _PutDecimal 	;c184
GetRandom		JMP _GetRandom		;c187
MouseUp 		JMP _MouseUp		;c18a
MouseOff		JMP _MouseOff		;c18d
DoPreviousMenu		JMP _DoPreviousMenu	;c190
ReDoMenu		JMP _ReDoMenu		;c193
GetSerialNumber 	JMP _GetSerialNumber	;c196
Sleep			JMP _Sleep		;c199
ClearMouseMode		JMP _ClearMouseMode	;c19c
i_Rectangle		JMP _i_Rectangle	;c19f
i_FrameRectangle	JMP _i_FrameRectangle	;c1a2
i_RecoverRectangle	JMP _i_RecoverRectangle ;c1a5
i_GraphicsString	JMP _i_GraphicsString	;c1a8
i_BitmapUp		JMP _i_BitmapUp 	;c1ab
i_PutString		JMP _i_PutString	;c1ae
GetRealSize		JMP _GetRealSize	;c1b1
i_FillRam		JMP _i_FillRam		;c1b4
i_MoveData		JMP _i_MoveData 	;c1b7
GetString		JMP _GetString		;c1ba
GotoFirstMenu		JMP _GotoFirstMenu	;c1bd
InitTextPrompt		JMP _InitTextPrompt	;c1c0
MainLoop		JMP _MainLoop		;c1c3
DrawSprite		JMP _DrawSprite 	;c1c6
GetCharWidth		JMP _GetCharWidth	;c1c9
LoadCharSet		JMP _LoadCharSet	;c1cc
PosSprite		JMP _PosSprite		;c1cf
EnablSprite		JMP _EnablSprite	;c1d2
DisablSprite		JMP _DisablSprite	;c1d5
CallRoutine		JMP _CallRoutine	;c1d8
CalcBlksFree		JMP (_CalcBlksFree)	;c1db
ChkDkGEOS		JMP (_ChkDkGEOS)	;c1de
NewDisk 		JMP (_NewDisk)		;c1e1
GetBlock		JMP (_GetBlock) 	;c1e4
PutBlock		JMP (_PutBlock) 	;c1e7
SetGEOSDisk		JMP (_SetGEOSDisk)	;c1ea
SaveFile		JMP _SaveFile		;c1ed
SetGDirEntry		JMP _SetGDirEntry	;c1f0
BldGDirEntry		JMP _BldGDirEntry	;c1f3
GetFreeDirBlk		JMP (_GetFreeDirBlk)	;c1f6
WriteFile		JMP _WriteFile		;c1f9
BlkAlloc		JMP (_BlkAlloc) 	;c1fc
ReadFile		JMP _ReadFile		;c1ff
SmallPutChar		JMP _SmallPutChar	;c202
FollowChain		JMP _FollowChain	;c205
GetFile 		JMP _GetFile		;c208
FindFile		JMP _FindFile		;c20b
CRC			JMP _CRC		;c20e
LdFile			JMP _LdFile		;c211
EnterTurbo		JMP (_EnterTurbo)	;c214
LdDeskAcc		JMP _LdDeskAcc		;c217
ReadBlock		JMP (_ReadBlock)	;c21a
LdApplic		JMP _LdApplic		;c21d
WriteBlock		JMP (_WriteBlock)	;c220
VerWriteBlock		JMP (_VerWriteBlock)	;c223
FreeFile		JMP _FreeFile		;c226
GetFHdrInfo		JMP _GetFHdrInfo	;c229
EnterDeskTop		JMP _EnterDeskTop	;c22c
StartAppl		JMP _StartAppl		;c22f
ExitTurbo		JMP (_ExitTurbo)	;c232
PurgeTurbo		JMP (_PurgeTurbo)	;c235
DeleteFile		JMP _DeleteFile 	;c238
FindFTypes		JMP _FindFTypes 	;c23b
RstrAppl		JMP _RstrAppl		;c23e
ToBASIC 		JMP _ToBASIC		;c241
FastDelFile		JMP _FastDelFile	;c244
GetDirHead		JMP (_GetDirHead)	;c247
PutDirHead		JMP (_PutDirHead)	;c24a
NxtBlkAlloc		JMP (_NxtBlkAlloc)	;c24d
ImprintRectangle	JMP _ImprintRectangle	;c250
i_ImprintRectangle	JMP _i_ImprintRectangle ;c253
DoDlgBox		JMP _DoDlgBox		;c256
RenameFile		JMP _RenameFile 	;c259
InitForIO		JMP (_InitForIO)	;c25c
DoneWithIO		JMP (_DoneWithIO)	;c25f
DShiftRight		JMP _DShiftRight	;c262
CopyString		JMP _CopyString 	;c265
CopyFString		JMP _CopyFString	;c268
CmpString		JMP _CmpString		;c26b
CmpFString		JMP _CmpFString 	;c26e
FirstInit		JMP _FirstInit		;c271
OpenRecordFile		JMP _OpenRecordFile	;c274
CloseRecordFile 	JMP _CloseRecordFile	;c277
NextRecord		JMP _NextRecord 	;c27a
PreviousRecord		JMP _PreviousRecord	;c27d
PointRecord		JMP _PointRecord	;c280
DeleteRecord		JMP _DeleteRecord	;c283
InsertRecord		JMP _InsertRecord	;c286
AppendRecord		JMP _AppendRecord	;c289
ReadRecord		JMP _ReadRecord 	;c28c
WriteRecord		JMP _WriteRecord	;c28f
SetNextFree		JMP (_SetNextFree)	;c292
UpdateRecordFile	JMP _UpdateRecordFile	;c295
GetPtrCurDkNm		JMP _GetPtrCurDkNm	;c298
PromptOn		JMP _PromptOn		;c29b
PromptOff		JMP _PromptOff		;c29e
OpenDisk		JMP (_OpenDisk) 	;c2a1
DoInlineReturn		JMP _DoInlineReturn	;c2a4
GetNextChar		JMP _GetNextChar	;c2a7
BitmapClip		JMP _BitmapClip 	;c2aa
FindBAMBit		JMP (_FindBAMBit)	;c2ad
SetDevice		JMP _SetDevice		;c2b0
IsMseInRegion		JMP _IsMseInRegion	;c2b3
ReadByte		JMP _ReadByte		;c2b6
FreeBlock		JMP (_FreeBlock)	;c2b9
ChangeDiskDevice	JMP (_ChangeDiskDevice) ;c2bc
RstrFrmDialogue 	JMP _RstrFrmDialogue	;c2bf
Panic			JMP _Panic		;c2c2
BitOtherClip		JMP _BitOtherClip	;c2c5
	!if (REUPresent) {
StashRAM		JMP _StashRAM		;c2c8
FetchRAM		JMP _FetchRAM		;c2cb
SwapRAM 		JMP _SwapRAM		;c2ce
VerifyRAM		JMP _VerifyRAM		;c2d1
DoRAMOp 		JMP _DoRAMOp		;c2d4
	} else {
StashRAM		LDX #DEV_NOT_FOUND	;c2c8
			RTS
FetchRAM		LDX #DEV_NOT_FOUND	;c2cb
			RTS
SwapRAM 		LDX #DEV_NOT_FOUND	;c2ce
			RTS
VerifyRAM		LDX #DEV_NOT_FOUND	;c2d1
			RTS
DoRAMOp 		LDX #DEV_NOT_FOUND	;c2d4
			RTS
	}
;here the JumpTable ends
;--------------------------------------------

DkNmTab 	!byte <DrACurDkNm, <DrBCurDkNm
		!byte <DrCCurDkNm, <DrDCurDkNm
		!byte >DrACurDkNm, >DrBCurDkNm
		!byte >DrCCurDkNm, >DrDCurDkNm

_InterruptMain
		JSR ProcessMouse		;c2d7
		JSR _ProcessTimers
		JSR _ProcessDelays
		JSR ProcessCursor
		JMP _GetRandom

_CallRoutine					;c59f
		CMP #0
		BNE CRou1
		CPX #0
		BEQ CRou2
CRou1		STA CallRLo			;c5a7
		STX CallRHi
		JMP (CallRLo)
CRou2		RTS				;c5ae

_DoInlineReturn 				;c5af
		+add returnAddress
		STA returnAddress
		BCC DILR1
		INC returnAddress+1
DILR1		PLP				;c5b8
		JMP (returnAddress)

SetVICRegs	STY r1L 			;c5bc
		LDY #0
SVR0		LDA (r0),Y			;c5c0
		CMP #$AA
		BEQ SVR1
		STA vicbase,Y
SVR1		INY				;c5c9
		CPY r1L
		BNE SVR0
		RTS

_InitRam	LDY #0				;c567
		LDA (r0),Y
		STA r1L
		INY
		ORA (r0),Y
		BEQ IRam3
		LDA (r0),Y
		STA r1H
		INY
		LDA (r0),Y
		STA r2L
		INY
IRam0		TYA				;c57c
		TAX
		LDA (r0),Y
		LDY #0
		STA (r1),Y
		INC r1L
		BNE IRam1
		INC r1H
IRam1		TXA				;c58a
		TAY
		INY
		DEC r2L
		BNE IRam0
		TYA
		+add r0L
		STA r0L
		BCC IRam2
		INC r0H
IRam2		+bra _InitRam			;c59b
IRam3		RTS				;c59e

InitGEOS	JSR _DoFirstInitIO		;c40a ;UNK_1
InitGEOEnv	LDA #>InitRamTab		;c40d ;UNK_1_1
		STA r0H
		LDA #<InitRamTab
		STA r0L
		JMP _InitRam

_DoFirstInitIO
		+LoadB CPU_DDR, $2f		;c436
		+LoadB CPU_DATA, KRNL_IO_IN
		LDX #7
		LDA #true
DFIIO0		STA KbdDMltTab,X		;c442
		STA KbdDBncTab,X
		DEX
		BPL DFIIO0
		STX KbdQueFlag
		STX cia1base+2
		INX
		STX KbdQueHead
		STX KbdQueTail
		STX cia1base+3
		STX cia1base+15
		STX cia2base+15
		LDA PALNTSCFLAG
		BEQ DFIIO1
		LDX #$80
DFIIO1		STX cia1base+14 		;c468
		STX cia2base+14
		LDA cia2base
		AND #%00110000
		ORA #%00000101
		STA cia2base
		+LoadB cia2base+2, $3f
		+LoadB cia1base+13, $7f
		STA cia2base+13
		+LoadW r0, VIC_IniTbl
		LDY #30
		JSR SetVICRegs
		JSR Init_KRNLVec
		+LoadB CPU_DATA, RAM_64K
		JMP ResetMseRegion

DeskTopStart	!word 0 	;these are for ensuring compatibility with
DeskTopExec	!word 0 	;DeskTop replacements - filename of desktop
DeskTopLgh	!byte 0 	;have to be at $c3cf		.IDLE

DeskTopName	!text "DESK TOP", NULL		;c3cf

;--------------------------------------------
;IMPORTANT! FROM NOW ON YOU CAN CHANGE THE CODE UNTIL FURTHER NOTICES.
;--------------------------------------------
_MainLoop
		JSR _DoCheckButtons		;c0df
		JSR _ExecuteProcesses
		JSR _DoCheckDelays
		JSR _DoUpdateTime
		LDA appMain+0
		LDX appMain+1
_MNLP		JSR CallRoutine 		;c0f1
		CLI

_MainLoop2	LDX CPU_DATA			;c313
		+LoadB CPU_DATA, IO_IN
		LDA grcntrl1
		AND #%01111111
		STA grcntrl1
		STX CPU_DATA
		JMP _MainLoop

BootKernal	+bbsf 5, sysFlgCopy, BootREU
		JSR $FF90
		LDA #version-bootName
		LDX #<bootName
		LDY #>bootName
		JSR $FFBD
		LDA #$50
		LDX #8
		LDY #1
		JSR $FFBA
		LDA #0
		JSR $FFD5
		BCC _RunREU
		JMP ($0302)
BootREU 	LDY #8				;c041
BootREU1	LDA BootREUTab,Y		;c043
		STA EXP_BASE+1,Y
		DEY
		BPL BootREU1
BootREU2	DEY				;c04c
		BNE BootREU2
_RunREU 	JMP RunREU			;c04f
BootREUTab
		!word $0091
		!word $0060
		!word $007e
		!word $0500
		!word $0000

	!if (removeToBASIC) {
_ToBASIC	sei
		jsr PurgeTurbo
		+LoadB CPU_DATA, KRNL_BAS_IO_IN
		+LoadB $DE00, 0
		jmp $fce2
	} else {
_ToBASIC	LDY #39 			;c05c
TB1		LDA (r0),Y			;c05e
		CMP #"A"
		BCC TB2
		CMP #"Z"+1
		BCS TB2
		SBC #$3F
TB2		STA LoKernalBuf,Y		;c06a
		DEY
		BPL TB1
		LDA r5H
		BEQ TB4
		INY
		TYA
TB3		STA BASICspace,Y		;c076
		INY
		BNE TB3
		+SubVW $0002,r7
		LDA (r7),Y
		PHA
		INY
		LDA (r7),Y
		PHA
		+PushW r7
		LDA (r5),Y
		STA r1L
		INY
		LDA (r5),Y
		STA r1H
		+LoadW r2, $ffff
		JSR _ReadFile
		+PopW r0
		LDY #1
		PLA
		STA (r0),Y
		DEY
		PLA
		STA (r0),Y
TB4		JSR GetDirHead			;c0b7
		JSR PurgeTurbo
		LDA sysRAMFlg
		STA sysFlgCopy
		AND #%00100000
		BEQ TB6
		LDY #6
TB5		LDA ToBASICTab,Y		;c0c9
		STA r0,Y
		DEY
		BPL TB5
		JSR StashRAM
TB6		JMP LoKernal1			;c0d5
ToBASICTab
		!word dirEntryBuf
		!word REUOsVarBackup
		!word OS_VARS_LGH
		!byte $00
	}

	!if (useRamExp) {
_EnterDeskTop					;c326
		SEI
		CLD
		LDX #TRUE
		STX firstBoot
		TXS
		JSR ClrScr
		JSR InitGEOS
		+MoveW DeskTopStart, r0
		+MoveB DeskTopLgh, r2H
		+LoadW r1, 1
		jsr RamExpRead
		+LoadB r0L, NULL
		+MoveW DeskTopExec, r7
	} else {
_EnterDT_DB					;c3c0
		!byte DEF_DB_POS | 1
		!byte DBTXTSTR, TXT_LN_X, TXT_LN_1_Y+6
		!word _EnterDT_Str0
		!byte DBTXTSTR, TXT_LN_X, TXT_LN_2_Y+6
		!word _EnterDT_Str1
		!byte OK, DBI_X_2, DBI_Y_2
		!byte NULL

_EnterDT_Str0					;c3d8
		!text BOLDON, "Please insert a disk", NULL
_EnterDT_Str1					;c3ee
		!text "with deskTop V1.5 or higher", NULL

_EnterDeskTop					;c326
		SEI
		CLD
		LDX #TRUE
		STX firstBoot
		TXS
		JSR ClrScr
		JSR InitGEOS
		+MoveB curDrive, TempCurDrive
		EOR #1
		TAY
		LDA _driveType,Y
		PHP
		LDA TempCurDrive
		PLP
		BPL EDT1
		TYA
EDT1		JSR EDT3			;c348
		LDY NUMDRV
		CPY #2
		BCC EDT2
		LDA curDrive
		EOR #1
		JSR EDT3
EDT2		+LoadW r0, _EnterDT_DB		;c35a
		JSR DoDlgBox
		LDA TempCurDrive
		BNE EDT1
EDT3		JSR SetDevice			;c36a
		JSR OpenDisk
		+beqx EDT5
EDT4		RTS				;c373
EDT5		STA r0L 			;c374
		+LoadW r6, DeskTopName
		JSR GetFile
		+bnex EDT4
		LDA fileHeader+O_GHFNAME+13
		CMP #"1"
		BCC EDT4
		BNE EDT6
		LDA fileHeader+O_GHFNAME+15
		CMP #"5"
		BCC EDT4
EDT6		LDA TempCurDrive		;c394
		JSR SetDevice
		+LoadB r0L, NULL
		+MoveW fileHeader+O_GHST_VEC, r7
	}
_StartAppl	SEI				;c3a8
		CLD
		LDX #$FF
		TXS
	JSR UNK_5
		JSR InitGEOS
		JSR _UseSystemFont
	JSR UNK_4
		LDX r7H
		LDA r7L
		JMP _MNLP

UNK_4	+MoveB A885D, r10L			;c5cf
	+MoveB A885E, r0L
		AND #1
		BEQ U_40
	+MoveW A885F, r7
U_40		+LoadW r2, dataDiskName 	;c5e7
		+LoadW r3, dataFileName
U_41		RTS


UNK_5	+MoveW r7, A885F
	+MoveB r10L, A885D
	+MoveB r0L, A885E
		AND #%11000000
		BEQ U_41
		LDY #>dataDiskName
		LDA #<dataDiskName
		LDX #r2
		JSR U_50
		LDY #>dataFileName
		LDA #<dataFileName
		LDX #r3
U_50		STY r4H 			;c61f
		STA r4L
		LDY #r4
		LDA #16
		jmp CopyFString

Init_KRNLVec
		LDX #32 			;c4da
IKV1		LDA KERNALVecTab-1,X		;c4dc
		STA irqvec-1,X
		DEX
		BNE IKV1
		RTS

_FirstInit					;c4e6
		SEI
		CLD
		JSR InitGEOS
		+LoadW EnterDeskTop+1, _EnterDeskTop
		+LoadB maxMouseSpeed, iniMaxMouseSpeed
		!if (iniMaxMouseSpeed=iniMouseAccel) {
		sta mouseAccel
		} else {
		+LoadB mouseAccel, iniMouseAccel
		}
		+LoadB minMouseSpeed, iniMinMouseSpeed
		+LoadB screencolors, (DKGREY << 4)+LTGREY
		STA FItempColor
		JSR i_FillRam
		!word 1000
		!word COLOR_MATRIX
FItempColor	!byte (DKGREY << 4)+LTGREY
		LDX CPU_DATA
		+LoadB CPU_DATA, IO_IN
		+LoadB mob0clr, BLUE
		STA mob1clr
		+LoadB extclr, BLACK
		STX CPU_DATA
		LDY #62
FI1		LDA #0				;c52b
		STA mousePicData,Y
		DEY
		BPL FI1
		LDX #24
FI2		LDA InitMsePic-1,X		;c535
		STA mousePicData-1,X
		DEX
		BNE FI2
	JMP UNK_6


_GetSerialNumber
		+LoadW r0, SerialNumber
		rts
;d000

_Panic						;cf88
		+PopW r0
		+SubVW 2, r0
		LDA r0H
		LDX #0
		JSR Panil0
		LDA r0L
		JSR Panil0
		+LoadW r0, _PanicDB_DT
		JSR DoDlgBox
Panil0		PHA				;cfb2
		LSR
		LSR
		LSR
		LSR
		JSR Panil1
		INX
		PLA
		AND #%00001111
		JSR Panil1
		INX
		RTS
Panil1		CMP #10 			;cfc3
		BCS Panil2
		+addv ("0")
		BNE Panil3
Panil2		+addv ("0"+7)			;cfcc
Panil3		STA _PanicAddy,X		;cfcf
		RTS


InitRamTab					;ddfa
		!word currentMode
		!byte 12
		!byte NULL
		!byte ST_WR_FORE | ST_WR_BACK
		!byte NULL
		!word mousePicData
		!byte NULL, SC_PIX_HEIGHT-1
		!word NULL, SC_PIX_WIDTH-1
		!byte NULL
		!word appMain
		!byte 28
		!word NULL, _InterruptMain
		!word NULL, NULL, NULL, NULL
		!word NULL, NULL, NULL, NULL
		!word _Panic, _RecoverRectangle
		!byte SelectFlashDelay, NULL
		!byte ST_FLASH, NULL
		!word NumTimers
		!byte 2
		!byte NULL, NULL
		!word clkBoxTemp
		!byte 1
		!byte NULL
		!word IconDescVecH
		!byte 1
		!byte NULL
		!word obj0Pointer
		!byte 8
		!byte $28, $29, $2a, $2b
		!byte $2c, $2d, $2e, $2f
		!word NULL


UNK_6		LDA #$bf			;e393
	STA A8FF0
		LDX #7
		LDA #$bb
UNK_61	STA A8FE8,x				;e39d
		DEX
		BPL UNK_61
		RTS

DBIcPicNO					;f914
	!byte 5, %########, $80+1, %#######., $db+8, 2, $80+6
	     ;%########, %########, %########, %########, %########, %#######.
	!byte %#......., %........, %........, %........, %........, %......##, $80+12
	     ;%#......., %........, %........, %........, %........, %......##
	!byte %#......., %.......#, %##..##.., %.#####.., %........, %......##
	!byte %#......., %.......#, %##..##.., %##...##., %........, %......##, $db+8, 2, $80+6
	!byte %#......., %.......#, %###.##.., %##...##., %........, %......##, $db+8, 2, $80+6
	     ;%#......., %.......#, %###.##.., %##...##., %........, %......##
	!byte %#......., %.......#, %#.####.., %##...##., %........, %......##, $db+8, 2, $80+6
	     ;%#......., %.......#, %#.####.., %##...##., %........, %......##
	!byte %#......., %.......#, %#..###.., %##...##., %........, %......##, $80+6
	     ;%#......., %.......#, %#..###.., %##...##., %........, %......##
	!byte %#......., %.......#, %#...##.., %.#####.., %........, %......##, $db+8, 2, $80+6
	!byte %#......., %........, %........, %........, %........, %......##
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%########, %########, %########, %########, %########, %########
	     ;%.#######, %########, %########, %########, %########, %########
	!byte 6, %########, $80+1, %.#######, 5, %########

DBIcPicYES					;f95f
	!byte 5, %########, $80+1, %#######., $db+8, 2, $80+6
	     ;%########, %########, %########, %########, %########, %#######.
	!byte %#......., %........, %........, %........, %........, %......##, $80+(5*6)
	     ;%#......., %........, %........, %........, %........, %......##
	!byte %#......., %....##.., %##..####, %##...###, %##......, %......## ;1
	!byte %#......., %....##.., %##..##.., %....##.., %.##....., %......## ;2
	!byte %#......., %....##.., %##..##.., %....##.., %........, %......## ;3
	!byte %#......., %.....###, %#...##.., %....##.., %........, %......## ;4
	!byte %#......., %.....###, %#...####, %#....###, %##......, %......##, $db+8, 2, $80+6
	!byte %#......., %......##, %....##.., %........, %.##....., %......##, $80+12
	     ;%#......., %......##, %....##.., %........, %.##....., %......##
	!byte %#......., %......##, %....##.., %....##.., %.##....., %......##
	!byte %#......., %......##, %....####, %##...###, %##......, %......##, $db+8, 2, $80+6
	!byte %#......., %........, %........, %........, %........, %......##
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%########, %########, %########, %########, %########, %########
	     ;%.#######, %########, %########, %########, %########, %########
	!byte 6, %########, $80+1, %.#######, 5, %########

DBIcPicOPEN					;f9b0
	!byte 5, %########, $80+1, %#######., $db+8, 2, $80+6
	     ;%########, %########, %########, %########, %########, %#######.
	!byte %#......., %........, %........, %........, %........, %......##, $80+(9*6)
	     ;%#......., %........, %........, %........, %........, %......##
	!byte %#......., %..#####., %........, %........, %........, %......## ;1
	!byte %#......., %.##...##, %........, %........, %........, %......## ;2
	!byte %#......., %.##...##, %.#####.., %.####..#, %####...., %......## ;3
	!byte %#......., %.##...##, %.##..##., %##..##.#, %##.##..., %......## ;4
	!byte %#......., %.##...##, %.##..##., %##..##.#, %#..##..., %......## ;5
	!byte %#......., %.##...##, %.##..##., %######.#, %#..##..., %......## ;6
	!byte %#......., %.##...##, %.##..##., %##.....#, %#..##..., %......## ;7
	!byte %#......., %.##...##, %.##..##., %##..##.#, %#..##..., %......## ;8
	!byte %#......., %..#####., %.#####.., %.####..#, %#..##..., %......##, $db+8, 2, $80+6
	!byte %#......., %........, %.##....., %........, %........, %......##
	     ;%#......., %........, %.##....., %........, %........, %......##
	     ;%########, %########, %########, %########, %########, %########
	     ;%.#######, %########, %########, %########, %########, %########
	!byte 6, %########, $80+1, %.#######, 5, %########

DBIcPicDISK					;fa03
	!byte 5, %########, $80+1, %#######., $db+8, 2, $80+6
	     ;%########, %########, %########, %########, %########, %#######.
	!byte %#......., %........, %........, %........, %........, %......##, $80+(9*6)
	     ;%#......., %........, %........, %........, %........, %......##
	!byte %#......., %...#####, %....##.., %......##, %........, %......## ;1
	!byte %#......., %...##..#, %#......., %......##, %........, %......## ;2
	!byte %#......., %...##..., %##.###.., %####..##, %..##...., %......## ;3
	!byte %#......., %...##..., %##..##.#, %#..##.##, %.##....., %......## ;4
	!byte %#......., %...##..., %##..##.#, %#.....##, %##......, %......## ;5
	!byte %#......., %...##..., %##..##.., %####..##, %#......., %......## ;6
	!byte %#......., %...##..., %##..##.., %...##.##, %##......, %......## ;7
	!byte %#......., %...##..#, %#...##.#, %#..##.##, %.##....., %......## ;8
	!byte %#......., %...#####, %....##.., %####..##, %..##...., %......##, $db+8, 2, $80+6
	!byte %#......., %........, %........, %........, %........, %......##
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%########, %########, %########, %########, %########, %########
	     ;%.#######, %########, %########, %########, %########, %########
	!byte 6, %########, $80+1, %.#######, 5, %########

;fa56
