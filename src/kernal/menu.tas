
;GEOS menu handler

;17-8-99 - ACME handler

_DoMenu
		STA menuOptionTab
		LDX #0
		STX menuNumber
		BEQ DoMenu1
DoMenu0 	LDX menuNumber
		LDA #NULL
		STA menuOptionTab,x
DoMenu1 	LDA r0L
		STA menuStackL,x
		LDA r0H
		STA menuStackH,x
		JSR GetMenuDesc
		SEC
DoMenu1_1	PHP
		+PushB dispBufferOn
		+LoadB dispBufferOn, ST_WR_FORE
		+PushW r11
		JSR CopyMenuCoords
		+PushW curPattern
		LDA #0
		JSR _SetPattern
		JSR _Rectangle
		+PopW curPattern
		LDA #$ff
		JSR _FrameRectangle
		+PopW r11
	JSR Menu_1
	!if ((menuVSeparator | menuHSeparator)!=0) {
		JSR DrawMenu
	}
		+PopB dispBufferOn
		PLP
		+bbsf 6, menuOptNumber, DoMenu2
		BCC DoMenu5
DoMenu2 	LDX menuNumber
		LDY menuOptionTab,x
		+bbsf 7, menuOptNumber, DoMenu3
		LDA menuLimitTabL,y
		STA r11L
		LDA menuLimitTabH,y
		STA r11H
		INY
		LDA menuLimitTabL,y
		CLC
		ADC r11L
		STA r11L
		LDA menuLimitTabH,y
		ADC r11H
		STA r11H
		ROR r11H
		ROR r11L
		LDA menuTop
		+add menuBottom
		ROR
		TAY
		+bra DoMenu4
DoMenu3 	LDA menuLimitTabL,y
		INY
		CLC
		ADC menuLimitTabL,y
		LSR
		TAY
		LDA menuLeft
		+add menuRight
		STA r11L
		LDA menuLeft+1
		ADC menuRight+1
		STA r11H
		LSR r11H
		ROR r11L
DoMenu4 	SEC
DoMenu5 	+bbrf MOUSEON_BIT, mouseOn, DoMenu6
		+smbf ICONSON_BIT, mouseOn
DoMenu6 	+smbf MENUON_BIT, mouseOn
		JMP _StartMouseMode

_ReDoMenu
		JSR _MouseOff
		JMP DoPrvMn1

_GotoFirstMenu
		PHP
		SEI
GFrstMenu1	+CmpBI menuNumber, 0
		BEQ GFrstMenu2
		JSR _DoPreviousMenu
		+bra GFrstMenu1
GFrstMenu2	PLP
		RTS

_DoPreviousMenu
		JSR _MouseOff
		JSR _RecoverMenu
		DEC menuNumber
DoPrvMn1	JSR GetMenuDesc
		CLC
		JMP DoMenu1_1

Menu_0		PHA
		LDY menuNumber
		LDA menuStackL,y
		STA r0L
		LDA menuStackH,y
		STA r0H
		+PopB r8L
		ASL
		ASL
		ADC r8L
		ADC #7
		TAY
Menu_01 	RTS

GetMenuDesc	LDX menuNumber
		LDA menuStackL,x
		STA r0L
		LDA menuStackH,x
		STA r0H
		LDY #6
		LDA (r0),y
		STA menuOptNumber
		DEY
GetMnuDsc1	LDA (r0),y
		STA mouseTop,y
		STA menuTop,y
		DEY
		BPL GetMnuDsc1
		+MoveW menuLeft, r11
		+MoveB menuTop, r1H
		+bbsf 6, menuOptNumber, Menu_01
		jmp ResetMseRegion

Menu_1		JSR MenuStoreFont
		JSR _UseSystemFont
		+LoadB r10H, 0
		STA currentMode
		SEC
	JSR Menu_4
Menu_11 JSR Menu_3
		CLC
	JSR Menu_4
	JSR Menu_2
		CLC
	JSR Menu_4
		+bbrf 7, menuOptNumber, Menu_12
		LDA r1H
		SEC
		ADC curHeight
		STA r1H
		+MoveW menuLeft, r11
		SEC
	JSR Menu_4
Menu_12 	+AddVB 1, r10H
		LDA menuOptNumber
		AND #%00011111
		CMP r10H
		BNE Menu_11
		JSR MenuRestoreFont
	JMP Menu_3

Menu_2		+PushW r10
		LDA r10H
	JSR Menu_0
		LDA (r0),y
		TAX
		INY
		LDA (r0),y
		STA r0H
		STX r0L
		+PushW leftMargin
		+PushW rightMargin
		+PushW StringFaultVec
		+LoadW leftMargin, 0
		SEC
		LDA menuRight
		SBC #1
		STA rightMargin
		LDA menuRight+1
		SBC #0
		STA rightMargin+1
		LDA #>MenuStringFault
		STA StringFaultVec+1
		LDA #<MenuStringFault
		STA StringFaultVec
		+PushB r1H
		+AddB baselineOffset, r1H
		INC r1H
		JSR _PutString
		+PopB r1H
		+PopW StringFaultVec
		+PopW rightMargin
		+PopW leftMargin
		+PopW r10
		RTS

MenuStringFault +MoveW mouseRight, r11
		RTS

Menu_3		LDY r10H
		LDX r1H
		+bbsf 7, menuOptNumber, Menu_31
		LDA r11H
		STA menuLimitTabH,y
		LDX r11L
Menu_31 	TXA
		STA menuLimitTabL,y
		RTS

Menu_4		BCC Menu_41
		+bbrf 7, menuOptNumber, Menu_42
		+bra Menu_43
Menu_41 	+bbrf 7, menuOptNumber, Menu_43
Menu_42 	+AddVB 2, r1H
		RTS
Menu_43 	+AddVW 4, r11
		RTS

_RecoverAllMenus
RcvrAllMns1	JSR GetMenuDesc
		JSR _RecoverMenu
		DEC menuNumber
		BPL RcvrAllMns1
		INC menuNumber
		RTS

_RecoverMenu
		JSR CopyMenuCoords
RcvrMnu0	LDA RecoverVector
		ORA RecoverVector+1
		BNE RcvrMnu1
		LDA #0
		JSR SetPattern
		JMP Rectangle
RcvrMnu1	JMP (RecoverVector)

	!if ((menuVSeparator | menuHSeparator)!=0) {
DrawMenu	LDA menuOptNumber
		AND #%00011111
		+subv 1
		BEQ DrawMenu4
		STA r2L
		+bbsf 7, menuOptNumber, DrawMenu2
	!if (menuVSeparator!=0) {
		LDA menuTop
		+addv 1
		STA r3L
		LDA menuBottom
		+subv 1
		STA r3H
DrawMenu1	LDX r2L
		LDA menuLimitTabL,x
		STA r4L
		LDA menuLimitTabH,x
		STA r4H
		LDA #menuVSeparator
		JSR _VerticalLine
		DEC r2L
		BNE DrawMenu1
	}
	!if (menuHSeparator!=0) {
		RTS
	}
DrawMenu2
	!if (menuHSeparator!=0) {
		+MoveW menuLeft, r3
		INC r3L
		BNE *+4
		INC r3H
		+MoveW menuRight, r4
		LDX #r4
		JSR Ddec
DrawMenu3	LDX r2L
		LDA menuLimitTabL,x
		STA r11L
		LDA #menuHSeparator
		JSR _HorizontalLine
		DEC r2L
		BNE DrawMenu3
	}
DrawMenu4	RTS
	}

CopyMenuCoords	LDX #6
CpyMnuCrds1	LDA menuTop-1,x
		STA r2-1,x
		DEX
		BNE CpyMnuCrds1
		RTS

	!if (oldMenu_5) {
Menu_5		JSR _MouseOff
		JSR Menu_7
		JSR MenuDoInvert
		LDA r9L
		LDX menuNumber
		STA menuOptionTab,x
		JSR Menu_8
		+bbsf 7, r1L, Menu_52
		BVS Menu_51
		+MoveB selectionFlash, r0L
		+LoadB r0H, NULL
		JSR _Sleep
		JSR Menu_7
		JSR MenuDoInvert
		+MoveB selectionFlash, r0L
		+LoadB r0H, NULL
		JSR _Sleep
		JSR Menu_7
	} else {
Menu_5Help	+MoveB selectionFlash, r0L
		+LoadB r0H, NULL
		JSR _Sleep
		JMP Menu_7

Menu_5		JSR _MouseOff
		JSR Menu_7
		JSR MenuDoInvert
		LDA r9L
		LDX menuNumber
		STA menuOptionTab,x
		JSR Menu_8
		+bbsf 7, r1L, Menu_52
		BVS Menu_51
		JSR Menu_5Help
		JSR MenuDoInvert
		JSR Menu_5Help
	}
		JSR MenuDoInvert
	JSR Menu_7
		LDX menuNumber
		LDA menuOptionTab,x
		PHA
	JSR Menu_8
		PLA
		JMP (r0)
		
Menu_51 JSR Menu_6
		LDA r0L
		ORA r0H
		BNE Menu_52
		RTS
Menu_52 	INC menuNumber
		JMP DoMenu0

Menu_6		LDX menuNumber
		LDA menuOptionTab,x
		PHA
	JSR Menu_8
		PLA
		JMP (r0)

Menu_7		LDA menuOptNumber
		AND #%00011111
		TAY
		LDA menuOptNumber
		BMI Menu_74
Menu_71 	DEY
		LDA mouseXPos+1
		CMP menuLimitTabH,y
		BNE Menu_72
		LDA mouseXPos
		CMP menuLimitTabL,y
Menu_72 	BCC Menu_71
		INY
		LDA menuLimitTabL,y
		STA r4L
		LDA menuLimitTabH,y
		STA r4H
		DEY
		LDA menuLimitTabL,y
		STA r3L
		LDA menuLimitTabH,y
		STA r3H
		STY r9L
		CPY #0
		BNE Menu_73
		INC r3L
		BNE *+4
		INC r3H
Menu_73 	LDX menuTop
		INX
		STX r2L
		LDX menuBottom
		DEX
		STX r2H
		RTS
Menu_74 	LDA mouseYPos
Menu_75 	DEY
		CMP menuLimitTabL,y
		BCC Menu_75
		INY
		LDA menuLimitTabL,y
		STA r2H
		DEY
		LDA menuLimitTabL,y
		STA r2L
		STY r9L
		CPY #0
		BNE Menu_76
		INC r2L
Menu_76 	+MoveW menuLeft, r3
		INC r3L
		BNE *+4
		INC r3H
		+MoveW menuRight, r4
		LDX #r4
		jmp Ddec

Menu_8	JSR Menu_0
		INY
		INY
		LDA (r0),y
		STA r1L
		INY
		LDA (r0),y
		TAX
		INY
		LDA (r0),y
		STA r0H
		STX r0L
		RTS

MenuDoInvert	+PushB dispBufferOn
		+LoadB dispBufferOn, ST_WR_FORE
		JSR _InvertRectangle
		+PopB dispBufferOn
		RTS

MenuStoreFont	LDX #9
MnuDStrFnt1	LDA baselineOffset-1,x
		STA saveFontTab-1,x
		DEX
		BNE MnuDStrFnt1
		RTS

MenuRestoreFont LDX #9
MnuDRstrFnt1	LDA saveFontTab-1,x
		STA baselineOffset-1,x
		DEX
		BNE MnuDRstrFnt1
		RTS
