
;multitasking functions (processes, sleep, delays)

;17-8-99 - ACME port

_InitProcesses
		LDX #0
		STX NumTimers
		STA r1L
		STA r1H
		TAX
		LDA #SET_FROZEN
IProc0		STA TimersCMDs-1,X
		DEX
		BNE IProc0
		LDY #0
IProc1		LDA (r0),Y
		STA TimersRtns,X
		INY
		LDA (r0),Y
		STA TimersRtns+1,X
		INY
		LDA (r0),Y
		STA TimersVals,X
		INY
		LDA (r0),Y
		STA TimersVals+1,X
		INY
		INX
		INX
		DEC r1H
		BNE IProc1
		+MoveB r1L, NumTimers
		RTS

_ExecuteProcesses
		LDX NumTimers
		BEQ EProc2
		DEX
EProc0		LDA TimersCMDs,X
		BPL EProc1
		AND #SET_BLOCKED
		BNE EProc1
		LDA TimersCMDs,X
		AND #SET_RUNABLE XOR $ff
		STA TimersCMDs,X
		TXA
		PHA
		ASL
		TAX
		LDA TimersRtns,X
		STA r0L
		LDA TimersRtns+1,X
		STA r0H
		JSR _DoExecProcess
		PLA
		TAX
EProc1		DEX
		BPL EProc0
EProc2		RTS
_DoExecProcess
		JMP (r0)

_ProcessTimers
		LDA #0
		TAY
		TAX
		CMP NumTimers
		BEQ PTime3
PTime0		LDA TimersCMDs,X
		AND #SET_FROZEN | SET_NOTIMER
		BNE PTime2
		LDA TimersTab,Y
		BNE PTime1
		PHA
		LDA TimersTab+1,Y
		+subv 1
		STA TimersTab+1,Y
		PLA
PTime1		+subv 1
		STA TimersTab,Y
		ORA TimersTab+1,Y
		BNE PTime2
		JSR RProc0
		LDA TimersCMDs,X
		ORA #SET_RUNABLE
		STA TimersCMDs,X
PTime2		INY
		INY
		INX
		CPX NumTimers
		BNE PTime0
PTime3		RTS

_RestartProcess
		LDA TimersCMDs,X
		AND #(SET_BLOCKED | SET_FROZEN) XOR $ff
		STA TimersCMDs,X
RProc0		TXA
		PHA
		ASL
		TAX
		LDA TimersVals,X
		STA TimersTab,X
		LDA TimersVals+1,X
		STA TimersTab+1,X
		PLA
		TAX
		RTS

_EnableProcess
		LDA TimersCMDs,X
		ORA #SET_RUNABLE
EnProc0 	STA TimersCMDs,X
		RTS

_BlockProcess
		LDA TimersCMDs,X
		ORA #SET_BLOCKED
		bne EnProc0
_UnBlockProcess
		LDA TimersCMDs,X
		AND #SET_BLOCKED XOR $ff
		+bra EnProc0
_FreezeProcess
		LDA TimersCMDs,X
		ORA #SET_FROZEN
		bne EnProc0
_UnFreezeProcess
		LDA TimersCMDs,X
		AND #SET_FROZEN XOR $ff
		+bra EnProc0

_ProcessDelays
		LDX DelaySP
		BEQ ProcDel3
		DEX
ProcDel0	LDA DelayValL,X
		BNE ProcDel1
		ORA DelayValH,X
		BEQ ProcDel2
		DEC DelayValH,X
ProcDel1	DEC DelayValL,X
ProcDel2	DEX
		BPL ProcDel0
ProcDel3	RTS


_DoCheckDelays
		LDX DelaySP
		BEQ DChDl2
		DEX
DChDl0		LDA DelayValL,X
		ORA DelayValH,X
		BNE DChDl1
		LDA DelayRtnsH,X
		STA r0H
		LDA DelayRtnsL,X
		STA r0L
		TXA
		PHA
		JSR _RemoveDelay
		JSR _DoExecDelay
		PLA
		TAX
DChDl1		DEX
		BPL DChDl0
DChDl2		RTS

_DoExecDelay
		INC r0L
		BNE DEDe0
		INC r0H
DEDe0		JMP (r0)

_RemoveDelay
		PHP
		SEI
RDel0		INX
		CPX DelaySP
		BEQ RDel1
		LDA DelayValL,X
		STA DelayValL-1,X
		LDA DelayValH,X
		STA DelayValH-1,X
		LDA DelayRtnsL,X
		STA DelayRtnsL-1,X
		LDA DelayRtnsH,X
		STA DelayRtnsH-1,X
		+bra RDel0
RDel1		DEC DelaySP
		PLP
		RTS

_Sleep		PHP
		PLA
		TAY
		SEI
		LDX DelaySP
		LDA r0L
		STA DelayValL,X
		LDA r0H
		STA DelayValH,X
		PLA
		STA DelayRtnsL,X
		PLA
		STA DelayRtnsH,X
		INC DelaySP
		TYA
		PHA
		PLP
		RTS
