
;font handling functions, mostly for internal (Kernal) use

;17-8-99 - ACME port

_GetRealSize
		+subv 32
		JSR GetChWdth1
		TAY
		TXA
		LDX curHeight
		PHA
		AND #$40
		BEQ GReSiz1
		INY
GReSiz1 	PLA
		AND #8
		BEQ GReSiz2
		INX
		INX
		INY
		INY
		LDA baselineOffset
		+addv 2
		RTS
GReSiz2 	LDA baselineOffset
		RTS

Font_1
		LDY r1H
		INY
	STY E87FE
		STA r5L
		LDX #0
		+addv 32
		JSR GetRealSize
		TYA
		PHA
		LDA r5L
		ASL
		TAY
		LDA (curIndexTable),y
		STA r2L
		AND #%00000111
	STA E87FD
		LDA r2L
		AND #%11111000
		STA r3L
		INY
		LDA (curIndexTable),y
		STA r2H
		PLA
		+add r2L
		STA r6H
		CLC
		SBC r3L
		LSR
		LSR
		LSR
		STA r3H
		TAX
		CPX #3
		BCC Font_11
		LDX #3
Font_11 	LDA Font_tabL,x
		STA r13L
		LDA Font_tabH,x
		STA r13H
		LDA r2L
		LSR r2H
		ROR
		LSR r2H
		ROR
		LSR r2H
		ROR
		+add cardDataPntr
		STA r2L
		LDA r2H
		ADC cardDataPntr+1
		STA r2H
	LDY E87FD
		LDA BitMask3,y
		EOR #$ff
	STA E87FC
		LDY r6H
		DEY
		TYA
		AND #%00000111
		TAY
		LDA BitMask4,y
		EOR #$ff
		STA r7H
		LDA currentMode
		TAX
		AND #SET_OUTLINE
		BEQ Font_12
		LDA #$80
Font_12 	STA r8H
		LDA r5L
		+addv 32
		JSR GetRealSize
		STA r5H
		+SubB r5H, r1H
		STX r10H
		TYA
		PHA
		LDA r11H
		BMI Font_13
		+CmpW rightMargin, r11
		BCC Font_16
Font_13 	LDA currentMode
		AND #SET_ITALIC
		BNE Font_14
		TAX
Font_14 	TXA
		LSR
		STA r3L
		+add r11L
		STA FontTVar2
		LDA r11H
		ADC #0
		STA FontTVar2+1
		+PopB PrvCharWidth
		+add FontTVar2
		STA r11L
		LDA #0
		ADC FontTVar2+1
		STA r11H
		BMI Font_17
		+CmpW leftMargin, r11
		BCS Font_17
	JSR Font_2
		LDX #0
		LDA currentMode
		AND #SET_REVERSE
		BEQ Font_15
		DEX
Font_15 	STX r10L
		CLC
		RTS

Font_16
		+PopB PrvCharWidth
		+add r11L
		STA r11L
		BCC Font_18
		INC r11H
		SEC
		RTS
Font_17
		+SubB r3L, r11L
		BCS Font_18
		DEC r11H
Font_18 	SEC
		RTS

Font_tabL	!byte <FontGt1, <FontGt2, <FontGt3, <FontGt4
Font_tabH	!byte >FontGt1, >FontGt2, >FontGt3, >FontGt4

Font_2
		LDX r1H
		JSR GetScanLine
		LDA FontTVar2
		LDX FontTVar2+1
		BMI Font_22
		CPX leftMargin+1
		BNE Font_21
		CMP leftMargin
Font_21 	BCS Font_23
Font_22 	LDX leftMargin+1
		LDA leftMargin
Font_23 	PHA
		AND #%11111000
		STA r4L
		CPX #0
		BNE Font_24
		CMP #$c0
		BCC Font_26
Font_24 	+subv $80
		PHA
		+AddVB $80, r5L
		STA r6L
		BCC Font_25
		INC r5H
		INC r6H
Font_25 	PLA
Font_26 	STA r1L
		+MoveB FontTVar2+1, r3L
		LSR r3L
		LDA FontTVar2
		ROR
		LSR r3L
		ROR
		LSR r3L
		ROR
		STA r7L
		LDA leftMargin+1
		LSR
		LDA leftMargin
		ROR
		LSR
		LSR
		+sub r7L
		BPL Font_27
		LDA #0
Font_27 	STA FontTVar1
		LDA FontTVar2
		AND #%00000111
		STA r7L
		PLA
		AND #%00000111
		TAY
		LDA BitMask3,y
		STA r3L
		EOR #$ff
		STA r9L
		LDY r11L
		DEY
		LDX rightMargin+1
		LDA rightMargin
		CPX r11H
		BNE Font_28
		CMP r11L
Font_28 	BCS Font_29
		TAY
Font_29 	TYA
		AND #%00000111
		TAX
		LDA BitMask4,x
		STA r4H
		EOR #$ff
		STA r9H
		TYA
		+sub r4L
		BPL Font_210
		LDA #0
Font_210	LSR
		LSR
		LSR
		+add FontTVar1
		STA r8L
		CMP r3H
		BCS Font_211
		LDA r3H
Font_211	CMP #3
		BCS Font_213
		CMP #2
		BNE Font_212
		LDA #1
Font_212	ASL
		ASL
		ASL
		ASL
		STA r12L
		LDA r7L
	+sub E87FD
		+addv 8
		+add r12L
		TAX
		LDA Font_tab2,x
		+addv <FontSH1
		TAY
		LDA #0
		ADC #>FontSH1
		BNE Font_214
Font_213	LDA #>FontSH5
		LDY #<FontSH5
Font_214	STA r12H
		STY r12L
Font_215	CLC
		RTS

Font_tab2
		!byte <(FntSh56-FontSH1)
		!byte <(FontSH3-FontSH1+0)
		!byte <(FontSH3-FontSH1+1)
		!byte <(FontSH3-FontSH1+2)
		!byte <(FontSH3-FontSH1+3)
		!byte <(FontSH3-FontSH1+4)
		!byte <(FontSH3-FontSH1+5)
		!byte <(FontSH3-FontSH1+6)
		!byte <(FontSH1-FontSH1+7)
		!byte <(FontSH1-FontSH1+6)
		!byte <(FontSH1-FontSH1+5)
		!byte <(FontSH1-FontSH1+4)
		!byte <(FontSH1-FontSH1+3)
		!byte <(FontSH1-FontSH1+2)
		!byte <(FontSH1-FontSH1+1)
		!byte <(FontSH1-FontSH1+0)
		!byte <(FntSh56-FontSH1)
		!byte <(FontSH4-FontSH1+0)
		!byte <(FontSH4-FontSH1+5)
		!byte <(FontSH4-FontSH1+10)
		!byte <(FontSH4-FontSH1+15)
		!byte <(FontSH4-FontSH1+20)
		!byte <(FontSH4-FontSH1+25)
		!byte <(FontSH4-FontSH1+30)
		!byte <(FontSH2-FontSH1+35)
		!byte <(FontSH2-FontSH1+30)
		!byte <(FontSH2-FontSH1+25)
		!byte <(FontSH2-FontSH1+20)
		!byte <(FontSH2-FontSH1+15)
		!byte <(FontSH2-FontSH1+10)
		!byte <(FontSH2-FontSH1+5)
		!byte <(FontSH2-FontSH1+0)

Font_3		LDA currentMode
		BPL Font_32
		LDY r1H
	CPY E87FE
		BEQ Font_31
		DEY
	CPY E87FE
		BNE Font_32
Font_31 	LDA r10L
		EOR #$ff
		STA r10L
Font_32 	+bbrf ITALIC_BIT, currentMode, Font_215
		LDA r10H
		LSR
		BCS Font_35
		LDX FontTVar2
		BNE Font_33
		DEC FontTVar2+1
Font_33 	DEX
		STX FontTVar2
		LDX r11L
		BNE Font_34
		DEC r11H
Font_34 	DEX
		STX r11L
	JSR Font_2
Font_35 	+CmpW rightMargin, FontTVar2
		BCC Font_36
		+CmpW leftMargin, r11
		RTS
Font_36 	SEC
		RTS

Font_4		LDY r1L
		LDX FontTVar1
		LDA Z45,x
		CPX r8L
		BEQ Font_43
		BCS Font_44
		EOR r10L
		AND r9L
		STA Font4_B1
		LDA r3L
		AND (r6),y
Font4_B1 = *+1
		ORA #0
		STA (r6),y
		STA (r5),y
Font_41 	TYA
		+addv 8
		TAY
		INX
		CPX r8L
		BEQ Font_42
		LDA Z45,x
		EOR r10L
		STA (r6),y
		STA (r5),y
		+bra Font_41
Font_42 	LDA Z45,x
		EOR r10L
		AND r9H
		STA Font4_B2
		LDA r4H
		AND (r6),y
Font4_B2 = *+1
		ORA #0
		STA (r6),y
		STA (r5),y
		RTS
Font_43 	EOR r10L
		AND r9H
		EOR #$ff
		ORA r3L
		ORA r4H
		EOR #$ff
		STA Font4_B3
		LDA r3L
		ORA r4H
		AND (r6),y
Font4_B3 = *+1
		ORA #0
		STA (r6),y
		STA (r5),y
Font_44 	RTS

Font_5		LDX r8L
		LDA #0
Font_51 STA E87FF,x
		DEX
		BPL Font_51
		LDA r8H
		AND #%01111111
		BNE Font_54
Font_52 JSR Font_8
Font_52_2	LDX r8L
Font_53 LDA E87FF,x
		STA Z45,x
		DEX
		BPL Font_53
		INC r8H
		RTS
Font_54 	CMP #1
		BEQ Font_55
		LDY r10H
		DEY
		BEQ Font_52
		DEY
		PHP
	JSR Font_8
	JSR Font_6
		PLP
		BEQ Font_56
Font_55 JSR Font_6
		JSR FntIndirectJMP
	JSR Font_8
		+SubW curSetWidth, r2
Font_56 	JSR FntIndirectJMP
	JSR Font_8
	JSR Font_7
		+bra Font_52_2

Font_6		+AddW curSetWidth, r2
		RTS

Font_7		LDY #$ff
Font_71 	INY
		LDX #7
Font_72 	LDA Z45,y
		AND BitMask2,x
		BEQ Font_73
		LDA BitMask2,x
		EOR #$ff
	AND E87FF,y
	STA E87FF,y
Font_73 	DEX
		BPL Font_72
		CPY r8L
		BNE Font_71
		RTS

Font_8	JSR Font_9
		LDY #$ff
Font_81 	INY
		LDX #7
Font_82 	LDA Z45,y
		AND BitMask2,x
		BEQ Font_87
	LDA E87FF,y
		ORA BitMask2,x
	STA E87FF,y
		INX
		CPX #8
		BNE Font_83
	LDA E87FE,y
		ORA #1
	STA E87FE,y
		BNE Font_84
Font_83 LDA E87FF,y
		ORA BitMask2,x
	STA E87FF,y
Font_84 	DEX
		DEX
		BPL Font_85
	LDA E8800,y
		ORA #$80
	STA E8800,y
		BNE Font_86
Font_85 LDA E87FF,y
		ORA BitMask2,x
	STA E87FF,y
Font_86 	INX
Font_87 	DEX
		BPL Font_82
		CPY r8L
		BNE Font_81
		RTS

Font_9		LSR Z45
		ROR Z45+1
		ROR Z45+2
		ROR Z45+3
		ROR Z45+4
		ROR Z45+5
		ROR Z45+6
		ROR Z45+7
		RTS

Font_10 
		TAY
		+PushB r1H
		TYA
	JSR Font_1
		BCS Font_108
Font_100	CLC
		LDA currentMode
		AND #SET_UNDERLINE | SET_ITALIC
		BEQ Font_101
	JSR Font_3
Font_101	PHP
		BCS Font_102
		JSR FntIndirectJMP
Font_102	+bbrf 7, r8H, Font_103
	JSR Font_5
		+bra Font_104
Font_103 JSR Font_6
Font_104	PLP
		BCS Font_106
		LDA r1H
		CMP windowTop
		BCC Font_106
		CMP windowBottom
		BCC Font_105
		BNE Font_106
Font_105 JSR Font_4
Font_106	INC r5L
		INC r6L
		LDA r5L
		AND #%00000111
		BNE Font_107
		INC r5H
		INC r6H
		+AddVB $38, r5L
		STA r6L
		BCC Font_107
		INC r5H
		INC r6H
Font_107	INC r1H 
		DEC r10H
		BNE Font_100
Font_108	+PopB r1H
		RTS

;procedures indexed from Font_Tab2, DO NOT CHANGE!

FontSH1 	LSR
		LSR
		LSR
		LSR
		LSR
		LSR
		LSR
		JMP FntShJump
FontSH2 	LSR
		ROR Z46
		ROR Z47
		LSR
		ROR Z46
		ROR Z47
		LSR
		ROR Z46
		ROR Z47
		LSR
		ROR Z46
		ROR Z47
		LSR
		ROR Z46
		ROR Z47
		LSR
		ROR Z46
		ROR Z47
		LSR
		ROR Z46
		ROR Z47
		JMP FntShJump
FontSH3 	ASL
		ASL
		ASL
		ASL
		ASL
		ASL
		ASL
		JMP FntShJump
FontSH4 	ASL Z47
		ROL Z46
		ROL
		ASL Z47
		ROL Z46
		ROL
		ASL Z47
		ROL Z46
		ROL
		ASL Z47
		ROL Z46
		ROL
		ASL Z47
		ROL Z46
		ROL
		ASL Z47
		ROL Z46
		ROL
		ASL Z47
		ROL Z46
		ROL
		JMP FntShJump
FontSH5 	STA Z45
		LDA r7L
	+sub E87FD
		BEQ FntSh52
		BCC FntSh53
		TAY
FntSh51 JSR Font_9
		DEY
		BNE FntSh51
FntSh52 	LDA Z45
		JMP FntShJump
FntSh53 LDA E87FD
		+sub r7L
		TAY
FntSh54 	ASL Z45+7
		ROL Z45+6
		ROL Z45+5
		ROL Z45+4
		ROL Z45+3
		ROL Z45+2
		ROL Z45+1
		ROL Z45
		DEY
		BNE FntSh54
		LDA Z45
FntShJump	STA Z45
		+bbrf BOLD_BIT, currentMode, FntSh56
		LDA #0
		PHA
		LDY #$ff
FntSh55 	INY
		LDX Z45,y
		PLA
		ORA ID100+$10,x
		STA Z45,y
		TXA
		LSR
		LDA #0
		ROR
		PHA
		CPY r8L
		BNE FntSh55
		PLA
FntSh56 	RTS

; end of indexed table, keep on changin'

FntIndirectJMP	LDY #0
		JMP (r13)

FontGt1 	STY Z45+1
		STY Z45+2
		LDA (r2),y
	AND E87FC
		AND r7H
		JMP (r12)

FontGt2 	STY Z45+2
		STY Z45+3
		LDA (r2),y
	AND E87FC
		STA Z45
		INY
		LDA (r2),y
		AND r7H
		STA Z45+1
FontGt2_1	LDA Z45
		JMP (r12)

FontGt3 	STY Z45+3
		STY Z45+4
		LDA (r2),y
	AND E87FC
		STA Z45
		INY
		LDA (r2),y
		STA Z45+1
		INY
		LDA (r2),y
		AND r7H
		STA Z45+2
		+bra FontGt2_1

FontGt4 	LDA (r2),y
	AND E87FC
		STA Z45
FontGt4_1	INY
		CPY r3H
		BEQ FontGt4_2
		LDA (r2),y
		STA Z45,y
		+bra FontGt4_1
FontGt4_2	LDA (r2),y
		AND r7H
		STA Z45,y
		LDA #0
		STA Z45+1,y
		STA Z45+2,y
		BEQ FontGt2_1
