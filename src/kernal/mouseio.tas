
;almost all about mouse, menu & icon mouse handling is elsewhere

;17-8-99 - ACME port

_IsMseInRegion
		LDA mouseYPos
		CMP r2L
		BCC IMIRl4
		CMP r2H
		BEQ IMIRl0
		BCS IMIRl4
IMIRl0		LDA mouseXPos+1
		CMP r3H
		BNE IMIRl1
		LDA mouseXPos
		CMP r3L
IMIRl1		BCC IMIRl4
		LDA mouseXPos+1
		CMP r4H
		BNE IMIRl2
		LDA mouseXPos
		CMP r4L
IMIRl2		BEQ IMIRl3
		BCS IMIRl4
IMIRl3		LDA #true
		RTS
IMIRl4		LDA #false
		RTS


_StartMouseMode
		BCC SMousMd1
		LDA r11L
		ORA r11H
		BEQ SMousMd1
		+MoveW r11, mouseXPos
		STY mouseYPos
		JSR SlowMouse
SMousMd1	LDA #>CheckClickPos
		STA mouseVector+1
		LDA #<CheckClickPos
		STA mouseVector
		LDA #>DoMouseFault
		STA mouseFaultVec+1
		LDA #<DoMouseFault
		STA mouseFaultVec
		+LoadB faultData, NULL
		JMP MouseUp

_ClearMouseMode
		+LoadB mouseOn, NULL
CMousMd1	+LoadB r3L, NULL
		JMP _DisablSprite

_MouseOff	+rmbf MOUSEON_BIT, mouseOn
		JMP CMousMd1

_MouseUp	+smbf MOUSEON_BIT, mouseOn
MseUp1		RTS

ProcessMouse	JSR UpdateMouse
		+bbrf MOUSEON_BIT, mouseOn, MseUp1
		JSR CheckMsePos
		+LoadB r3L, 0
		+MoveW msePicPtr, r4
		JSR DrawSprite
		+MoveW mouseXPos, r4
		+MoveB mouseYPos, r5L
		JSR PosSprite
		jmp EnablSprite

CheckMsePos	LDY mouseLeft
		LDX mouseLeft+1
		LDA mouseXPos+1
		BMI ChMsePs2
		CPX mouseXPos+1
		BNE ChMsePs1
		CPY mouseXPos
ChMsePs1	BCC ChMsePs3
		BEQ ChMsePs3
ChMsePs2	+smbf OFFLEFT_BIT, faultData
		STY mouseXPos
		STX mouseXPos+1
ChMsePs3	LDY mouseRight
		LDX mouseRight+1
		CPX mouseXPos+1
		BNE ChMsePs4
		CPY mouseXPos
ChMsePs4	BCS ChMsePs5
		+smbf OFFRIGHT_BIT, faultData
		STY mouseXPos
		STX mouseXPos+1
ChMsePs5	LDY mouseTop
		+CmpBI mouseYPos, 228
		BCS ChMsePs6
		CPY mouseYPos
		BCC ChMsePs7
		BEQ ChMsePs7
ChMsePs6	+smbf OFFTOP_BIT, faultData
		STY mouseYPos
ChMsePs7	LDY mouseBottom
		CPY mouseYPos
		BCS ChMsePs8
		+smbf OFFBOTTOM_BIT, faultData
		STY mouseYPos
ChMsePs8	+bbrf MENUON_BIT, mouseOn, ChMsePs11
		LDA mouseYPos
		CMP menuTop
		BCC ChMsePs10
		CMP menuBottom
		BEQ ChMsePs9
		BCS ChMsePs10
ChMsePs9	+CmpW mouseXPos, menuLeft
		BCC ChMsePs10
		+CmpW mouseXPos, menuRight
		BCC ChMsePs11
		BEQ ChMsePs11
ChMsePs10	+smbf OFFMENU_BIT, faultData
ChMsePs11	RTS

CheckClickPos	LDA mouseData
		BMI ChClkPos4
		LDA mouseOn
		AND #SET_MSE_ON
		BEQ ChClkPos4
		LDA mouseOn
		AND #SET_MENUON
		BEQ ChClkPos3
		+CmpB mouseYPos, menuTop
		BCC ChClkPos3
		CMP menuBottom
		BEQ ChClkPos1
		BCS ChClkPos3
ChClkPos1	+CmpW mouseXPos, menuLeft
		BCC ChClkPos3
		+CmpW mouseXPos, menuRight
		BEQ ChClkPos2
		BCS ChClkPos3
ChClkPos2 JMP Menu_5
ChClkPos3	+bbrf ICONSON_BIT, mouseOn, ChClkPos4
		JMP ProcessClick
ChClkPos4	LDA otherPressVec
		LDX otherPressVec+1
		JMP CallRoutine

DoMouseFault	+bbrf MOUSEON_BIT, mouseOn, ChMsePs11
		BVC ChMsePs11
		LDA menuNumber
		BEQ ChMsePs11
		+bbsf OFFMENU_BIT, faultData, DoMseFlt2
		LDX #SET_OFFTOP
		LDA #$C0
		TAY
		+bbsf 7, menuOptNumber, DoMseFlt1
		LDX #SET_OFFLEFT
DoMseFlt1	TXA
		AND faultData
		BNE DoMseFlt2
		TYA
		+bbsf 6, menuOptNumber, ChMsePs11
DoMseFlt2	jmp _DoPreviousMenu

ResetMseRegion	LDA #NULL
		STA mouseLeft
		STA mouseLeft+1
		STA mouseTop
		+LoadW mouseRight, SC_PIX_WIDTH-1
		+LoadB mouseBottom, SC_PIX_HEIGHT-1
		RTS

_DoCheckButtons
		+bbrf INPUT_BIT, pressFlag, DoChkBtns1
		+rmbf INPUT_BIT, pressFlag
		LDA inputVector
		LDX inputVector+1
		JSR CallRoutine
DoChkBtns1	+bbrf MOUSE_BIT, pressFlag, DoChkBtns2
		+rmbf MOUSE_BIT, pressFlag
		LDA mouseVector
		LDX mouseVector+1
		JSR CallRoutine
DoChkBtns2	+bbrf KEYPRESS_BIT, pressFlag, DoChkBtns3
		JSR KbdScanHelp3
		LDA keyVector
		LDX keyVector+1
		JSR CallRoutine
DoChkBtns3	LDA faultData
		BEQ DoChkBtns4
		LDA mouseFaultVec
		LDX mouseFaultVec+1
		JSR CallRoutine
		LDA #NULL
		STA faultData
DoChkBtns4	RTS
