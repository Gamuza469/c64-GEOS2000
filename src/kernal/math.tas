
;various math routines

;17-8-99 - ACME port

_DShiftLeft
		DEY
		BMI DShLf0
		ASL zpage,X
		ROL zpage+1,X
		JMP _DShiftLeft
DShLf0		RTS
_DShiftRight
		DEY
		BMI DShRg0
		LSR zpage+1,X
		ROR zpage,X
		JMP _DShiftRight
DShRg0		RTS

_BBMult
		LDA zpage,Y
		STA r8H
		STY r8L
		LDY #8
		LDA #0
BBMul0		LSR r8H
		BCC BBMul1
		CLC
		ADC zpage,X
BBMul1		ROR
		ROR r7L
		DEY
		BNE BBMul0
		STA zpage+1,X
		LDA r7L
		STA zpage,X
		LDY r8L
		RTS

_BMult
		LDA #0
		STA zpage+1,Y
_DMult		+LoadB r8L, 16
		+LoadW r7, 0
BMult0		LSR zpage+1,X
		ROR zpage,X
		BCC BMult1
		LDA r7L
		CLC
		ADC zpage,Y
		STA r7L
		LDA r7H
		ADC zpage+1,Y
BMult1		LSR
		STA r7H
		ROR r7L
		ROR r6H
		ROR r6L
		DEC r8L
		BNE BMult0
		LDA r6L
		STA zpage,X
		LDA r6H
		STA zpage+1,X
		RTS

_Ddiv
		+LoadW r8, 0
		+LoadB r9L, 16
Ddivl0		ASL zpage,X
		ROL zpage+1,X
		ROL r8L
		ROL r8H
		LDA r8L
		SEC
		SBC zpage,Y
		STA r9H
		LDA r8H
		SBC zpage+1,Y
		BCC Ddivl1
		INC zpage,X
		STA r8H
		LDA r9H
		STA r8L
Ddivl1		DEC r9L
		BNE Ddivl0
Ddivl2		RTS

_DSDiv
		LDA zpage+1,X
		EOR zpage+1,Y
		PHP
		JSR _Dabs
		STX r8L
		TYA
		TAX
		JSR _Dabs
		LDX r8L
		JSR _Ddiv
		PLP
		BPL Ddivl1
		jmp _Dnegate

_Dabs
		LDA zpage+1,X
		BMI _Dnegate
		RTS
_Dnegate	LDA zpage+1,X
		EOR #$FF
		STA zpage+1,X
		LDA zpage,X
		EOR #$FF
		STA zpage,X
		INC zpage,X
		BNE Dnegate1
		INC zpage+1,X
Dnegate1	RTS

_Ddec
		LDA zpage,X
		BNE Ddecl0
		DEC zpage+1,X
Ddecl0		DEC zpage,X
		LDA zpage,X
		ORA zpage+1,X
		RTS

_GetRandom
		INC random
		BNE GRandl0
		INC random+1
GRandl0 	ASL random
		ROL random+1
		BCC GRandl2
		LDA #$0F
		+add random
		STA random
		BCC GRandl1
		INC random+1
GRandl1 	RTS
GRandl2 	+CmpBI random+1, $ff
		BCC GRandl3
		LDA random
		+subv $f1
		BCC GRandl3
		STA random
		LDA #0
		STA random+1
GRandl3 	RTS

_CRC
		LDY #$ff
		STY r2L
		STY r2H
		INY
CRC1		LDA #$80
		STA r3L
CRC2		ASL r2L
		ROL r2H
		LDA (r0),y
		AND r3L
		BCC CRC3
		EOR r3L
CRC3		BEQ CRC4
		LDA r2L
		EOR #$21
		STA r2L
		LDA r2H
		EOR #$10
		STA r2H
CRC4		LSR r3L
		BCC CRC2
		INY
		BNE CRC5
		INC r0H
CRC5		LDX #r1
		JSR Ddec
		LDA r1L
		ORA r1H
		BNE CRC1
		RTS

ConvertBCD	PHA
		AND #%11110000
		LSR
		LSR
		LSR
		LSR
		TAY
		PLA
		AND #%00001111
		CLC
CvtBCD1 	DEY
		BMI CvtBCD2
		ADC #10
		BNE CvtBCD1
CvtBCD2 	RTS
